buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '[6.0,6.2)', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}

plugins {
    id 'eclipse'
    id 'maven-publish'
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

mixin {
    add sourceSets.main, "crust.mixins.refmap.json"
}


// The mod's version and id
version = "${mc_version}-${mod_version}"
archivesBaseName = 'crust'
group = "fathertoast.${archivesBaseName}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'official', version: "${mc_version}"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        configureEach {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                crust {
                    source sourceSets.main
                }
            }
        }

        client {
            property "mixin.debug.export", "true"
            property 'mixin.env.disableRefMap', 'true'

            arg "-mixin.config=crust.mixins.json"
        }

        server {
            property "mixin.debug.export", "true"
            property 'mixin.env.disableRefMap', 'true'

            arg "-mixin.config=crust.mixins.json"
        }

        data {
            args '--mod', 'crust', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name 'CurseMaven'
        url 'https://www.cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
    // Apocalypse isn't hosted on any maven servers anymore, oh well
    flatDir {
        dir 'flat_deps'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    // Group ID must be present for flat dir repositories, but are effectively ignored
    runtimeOnly fg.deobf("blank:Apocalypse-Rebooted:${mc_version}-${apocalypse_version}")
    compileOnly fg.deobf("blank:Apocalypse-Rebooted:${mc_version}-${apocalypse_version}:api")

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "Crust",
                "Specification-Vendor"    : "FatherToast",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "FatherToast",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "crust.mixins.json"
        ])
    }
}

task srcJar(type: Jar) {
    archiveClassifier.set('src')

    // Include the entire mod (deobf) plus java files
    from sourceSets.main.output
    from sourceSets.main.allSource
}

task apiJar(type: Jar) {
    archiveClassifier.set('api')

    // Include only the api package
    include 'fathertoast/crust/api/**/*'

    from sourceSets.main.output
    from sourceSets.main.allSource
}

artifacts {
    archives srcJar
    archives apiJar
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact apiJar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}